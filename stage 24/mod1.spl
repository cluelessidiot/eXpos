alias functionNum R1;
alias pid R2;
if (functionNum == 1) then		//Get pcb entry
	alias counterpid R3;
	counterpid = 1;
	while (counterpid < 16) do
		if ([PROCESS_TABLE + (counterpid * 16) + 4] == TERMINATED) then
			R0 = counterpid;		//return value
			[PROCESS_TABLE + (counterpid * 16) + 1] = counterpid;
			[PROCESS_TABLE + (counterpid * 16) + 14] = PAGE_TABLE_BASE + 20 * counterpid;
			[PROCESS_TABLE + (counterpid * 16) + 15] = 10;
			return;
		endif;
		counterpid = counterpid + 1;
	endwhile;
	R0 = -1;
	return;
endif;
if (functionNum == 2) then		//Free User Area Page
	alias UserAreaPage R3;
	alias perprocesstable R4;
	alias counter R5;
	UserAreaPage = [PROCESS_TABLE + (pid * 16) + 11];
	perprocesstable = UserAreaPage * 512 + (512 - 16);
	counter = 0;
	while (counter < 16) do
		if([perprocesstable + counter] == SEMAPHORE) then
			multipush(R1,R2,R3,R4,R5);
			R1 = 7;
			R3 = R2;									//pid stored in R2 (just a rearange)
			R2 = [perprocesstable + counter + 1];
			call MOD_0;									//release semaphore
			multipop(R1,R2,R3,R4,R5);
		endif;
		if([perprocesstable + counter] == FILE) then
			multipush(R1,R2,R3,R4,R5);
			R1 = 4;									
			R2 = [perprocesstable + counter + 1];
			call MOD_3;									//close
			multipop(R1,R2,R3,R4,R5);
		endif;
		counter = counter + 2;
	endwhile;
	R1 = 2;
	R2 = UserAreaPage;
	call MOD_2;
	return;
endif;
if (functionNum == 3) then			//Exit
	if ([PROCESS_TABLE + ([SYSTEM_STATUS_TABLE + 1] * 16) + 9] != 9) then
		alias counter R5;
		counter = 1;
		while (counter < 16) do
			if ([PROCESS_TABLE + (counter * 16) + 4] == WAIT_PROCESS && [PROCESS_TABLE + (counter * 16) + 5] == [SYSTEM_STATUS_TABLE + 1]) then
				[PROCESS_TABLE + (counter * 16) + 4] = READY;
			endif;
			if ([PROCESS_TABLE + (counter * 16) + 2] == [SYSTEM_STATUS_TABLE + 1]) then
				[PROCESS_TABLE + (counter * 16) + 2] = -1;
			endif;
			counter = counter + 1;
		endwhile;
	endif;
	multipush(R1,R2);
	R1 = 4;			//Free page table
	R2 = pid;
	call MOD_1;
	R1 = 2;			//Free User area page
	R2 = pid;
	call MOD_1;
	multipop(R1,R2);
	[PROCESS_TABLE + (pid * 16) + 4] = TERMINATED;
	return;
endif;
if (functionNum == 4) then			//Free Page Table
	alias ptbr R3;
	alias ptlr R4;
	ptbr = [PROCESS_TABLE + (pid * 16) + 14];
	ptlr = [PROCESS_TABLE + (pid * 16) + 15];
	alias counter R5;
	counter = 0;
	while (counter < ptlr) do
		if ([ptbr+ 2 * counter] != -1) then
			multipush(R1,R2,R3,R4,R5);
			R1 = 2;
			R2 = [ptbr+ 2 * counter];
			call MOD_2;
			multipop(R1,R2,R3,R4,R5);
		endif;
		[ptbr+ 2 * counter] = -1;
		[ptbr+ 2 * counter + 1] = "0000";
		counter = counter + 1;
	endwhile;
	counter = 0;			//2,3,8,9
	while(counter < 10) do
		if (counter == 2 || counter == 3 || counter == 8 || counter == 9) then
			if ([DISK_MAP_TABLE + pid*10 + counter] != -1) then
				multipush(R1,R2,R5);
				R1 = 4;
				R3 = R2;
				R2 = [DISK_MAP_TABLE + pid*10 + counter];
				call MOD_2;
				multipop(R1,R2,R5);
			endif;
		endif;
		[DISK_MAP_TABLE + pid*10 + counter] = -1;
		counter = counter + 1;
	endwhile;
	return;
endif;